# --------------------------------------------------------------------------- #
#    Package configuration file                                               #
#                                                                             #
#    This file contains instructions on how to generate ***Config.cmake,      #
#    that is, the file that tells CMake how the package should be             #
#    configured when found by other projects using find_package().            #
#                                                                             #
#    ***Targets.cmake (to be found alongside this file) is the target import  #
#    file that tells where the headers and library are. It will be different  #
#    depending on a local build or an installed package.                      #
#                                                                             #
#                              Niccolo' Iardella                              #
#                                Donato Meoli                                 #
#                         Dipartimento di Informatica                         #
#                             Universita' di Pisa                             #
# --------------------------------------------------------------------------- #
get_filename_component(@modName@_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
include(CMakeFindDependencyMacro)

# ----- Settings ------------------------------------------------------------ #
set(MILPSolver_USE_CPLEX @MILPSolver_USE_CPLEX@)
set(CPLEX_ROOT @CPLEX_ROOT@)

set(MILPSolver_USE_SCIP @MILPSolver_USE_SCIP@)
set(SCIP_ROOT @SCIP_ROOT@)

set(MILPSolver_USE_GUROBI @MILPSolver_USE_GUROBI@)
set(GUROBI_ROOT @GUROBI_ROOT@)

set(MILPSolver_USE_HiGHS @MILPSolver_USE_HiGHS@)
set(HiGHS_ROOT @HiGHS_ROOT@)

# ----- Requirements -------------------------------------------------------- #
find_dependency(SMS++)

# This is needed to find the custom modules we provide
list(APPEND CMAKE_MODULE_PATH ${@modName@_CMAKE_DIR})

if (MILPSolver_USE_SCIP)
    find_dependency(SCIP)
endif ()

if (MILPSolver_USE_CPLEX)
    find_dependency(CPLEX)
endif ()

if (MILPSolver_USE_GUROBI)
    find_dependency(GUROBI)
endif ()

if (MILPSolver_USE_HiGHS)
    find_dependency(HiGHS)
endif ()

list(REMOVE_AT CMAKE_MODULE_PATH -1)

# ----- Import target ------------------------------------------------------- #
if (NOT TARGET @modNamespace@::@modName@)
    include("${@modName@_CMAKE_DIR}/@modName@Targets.cmake")
endif ()

set(@modName@_LIBRARIES @modNamespace@::@modName@)

# --------------------------------------------------------------------------- #
